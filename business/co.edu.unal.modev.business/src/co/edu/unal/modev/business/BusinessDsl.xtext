grammar co.edu.unal.modev.business.BusinessDsl with org.eclipse.xtext.common.Terminals

generate businessDsl "http://www.edu.co/unal/modev/business/BusinessDsl"

BusinessModule:
	"business-module" name=ID "{"
		business+=Business*
	"}"
;
	
FQN: ID ("." ID)*;
	
Business:
	'business' name=ID
	'{'
		(operations += BusinessOperation)*
	'}'
;

BusinessOperation:
	'operation' name=ID  
		('returnType' returnType=BusinessReturnType)?
		('httpVerb' httpVerb=HTTP_VERB)? 
	(	'{'
				(parameters += BusinessParameter)+
		'}'
	)?
;

BusinessReturnType:
	voidType=VOID | entityType=BusinessType
;

BusinessParameter:
	'parameter' name=STRING 
		'httpType' httpType=HTTP_TYPE
		('type' type=BusinessType)?
;

BusinessType:
	collectionType=BusinessCollectionType | type=FQN 
;

BusinessCollectionType:
	'List<' listType=BusinessType '>'
;

LiteralType:
	literal = STRING
;

enum VOID:
	VOID='void'
;

enum HTTP_VERB:
	GET | POST | PUT | DELETE | OPTIONS | TRACE
;

enum HTTP_TYPE:
	QUERY | BODY | ROUTE_PARAM | FILE
;