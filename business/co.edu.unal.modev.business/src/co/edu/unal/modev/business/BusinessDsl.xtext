grammar co.edu.unal.modev.business.BusinessDsl with org.eclipse.xtext.common.Terminals

import "http://www.edu.co/unal/modev/dto/DtoDsl" as dtoDsl

generate businessDsl "http://www.edu.co/unal/modev/business/BusinessDsl"

BusinessModule:
	"business-module" name=ID "{"
		business+=Business*
	"}"
;
	
FQN: ID ("." ID)*;
	
Business:
	'business' name=ID 'description' description=STRING
	'{'
		(operations += BusinessOperation)*
	'}'
;

BusinessOperation:
	'operation' name=ID  
		'returnType' returnType=BusinessReturnType (many?='*')?
		'description' description=STRING
	(	'{'
				(parameters += BusinessParameter)*
		'}'
	)?
;

BusinessReturnType:
	void=VOID | businessType=BusinessType
;

BusinessParameter:
	'parameter' name=FQN
		'type' type=BusinessType (many?='*')?
		(required?='required')?
		'description' description=STRING
;

BusinessType:
	simple=SimpleBusinessTypes | dtoType=[dtoDsl::Dto | FQN] | literalType=STRING
;

enum VOID:
	VOID='void'
;

enum SimpleBusinessTypes:
	STRING='String' |
	NUMBER='Number' |
	DECIMAL='Decimal' |
	DATE='Date' |
	BOOLEAN='Boolean';