grammar co.edu.unal.modev.route.RouteDsl with org.eclipse.xtext.common.Terminals

import "http://www.edu.co/unal/modev/business/BusinessDsl" as business

generate routeDsl "http://www.edu.co/unal/modev/route/RouteDsl"

RoutesModule:
	"routes-module" name=ID "{"
		resourcesContext+=ResourcesContext*
	"}"
;
	
FQN: ID ("." ID)*;

ResourcesContext:
	'resource-context' name=ID 'basePath' basePath=STRING 'description' description=STRING '{'
		routes+=Route*
	'}'
;
	
Route:
	'route' name=ID '{'
		"uri" uri=STRING
		'httpVerb' httpVerb=HTTP_VERB
		"operation" operation=[business::BusinessOperation | FQN]
		'parameters' '{'
			(parameters += HttpParameter)*
		'}'
		'responseMessages' '{'
			(responseMessages += ResponseMessage)*
		'}'
	'}'
;

ResponseMessage:
	'code' code=INT
	'message' message=STRING
;

HttpParameter:
	'parameter' param=[business::BusinessParameter | FQN] 
	'httpType' httpType=HTTP_TYPE
;

enum HTTP_VERB:
	GET | POST | PUT | DELETE | OPTIONS | TRACE
;

enum HTTP_TYPE:
	QUERY | BODY | FILE | NAMED
;