grammar co.edu.unal.modev.entity.EntityDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate entityDsl "http://www.edu.co/unal/modev/entity/EntityDsl"

EntitiesModule:
	"entities-module" name=ID "{"
		entities+=Entity*
	"}"
;

FQN: ID ("." ID)*;

Entity:
	"entity" name=FQN "{"
		("tableName" tableName=STRING)?
		
		columns+=Column*
		
		"primary-keys" "{"
			primaryKeys+=PrimaryKey*
		"}"
		
		("relations" "{"
			relations+=Relation*
		"}")?
		
	"}"
;

PrimaryKey:
	name=[Column | ID] ("autoIncrement" autoIncrement=BOOLEAN)?
;

Relation:
	HasOneRelation | HasManyRelation | BelongsToRelation
;

HasOneRelation:
	"hasOne" name=[Entity | FQN] ("foreignKey" foreignKey=STRING)? ("as" asValue=STRING)?
;

HasManyRelation:
	"hasMany" name=[Entity | FQN] ("foreignKey" foreignKey=STRING)? ("as" asValue=STRING)?
;

BelongsToRelation:
	"belongsTo" name=[Entity | FQN] ("foreignKey" foreignKey=STRING)? ("as" asValue=STRING)?
;

Column:
	"column" name=ID "{"
		("columnName" columnName=STRING)?
		"type" type=DATATYPE_ENUM ("length" length=INT ("precision" precision=INT)?)?
		("databaseType" databaseType=STRING)?
		("languageType" languageType=STRING)?
		("transient" transient=BOOLEAN)?
		("allowNull" allowNull=BOOLEAN)?
		("defaultValue" defaultValue=STRING)?
		("comment" comment=STRING)?
	"}"
;

enum DATATYPE_ENUM:
	STRING ='STRING' | 
	TEXT = 'TEXT' | 
	INTEGER = 'INTEGER' | 
	BIGINT = 'BIGINT' |
	FLOAT = 'FLOAT' |
	DECIMAL = 'DECIMAL' |
	DATE = 'DATE' |
	BOOLEAN = 'BOOLEAN' |
	ENUM = 'ENUM' |
	BLOB = 'BLOB'
;

terminal BOOLEAN returns ecore::EBoolean:
    'true' | 'false' | 'TRUE' | 'FALSE'
;