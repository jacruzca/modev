grammar co.edu.unal.modev.document.DocumentDsl with org.eclipse.xtext.common.Terminals

generate documentDsl "http://www.edu.co/unal/modev/document/DocumentDsl"

DocumentsModule:
	"documents-module" name=ID "{"
		documents+=Document*
	"}";

FQN:
	ID ("." ID)*;

Document:
	"schema" name=FQN "{"
		properties+=DocumentProperty*
		("options" "{"
			(autoIndex?="autoIndex")?
			(bufferCommands?="bufferCommands")?
			(capped?="capped")?
			(id?="id")?
			(minimize?="minimize")?
			(safe?="safe")?
			(shardKey?="shardKey")?
			(strict?="strict")?
			(versionKey?="versionKey")?
			("collection" collection=STRING)?
			("read" read=STRING)?
		"}")?
	"}";

DocumentProperty:
	name=ID type=AbstractType 
	(many?='*')? 
	("default" defaultValue=STRING)?
	(unique?='unique')?
	(index?='index')?
;
	
AbstractType:
	simple=SimpleTypes | inline=Document | ref= RefType
;
	
RefType:
	"ref" ref=[Document | FQN]
;

enum SimpleTypes:
	STRING = 'String' |
	NUMBER = 'Number' |
	DATE = 'Date' |
	BOOLEAN = 'Boolean' |
	BUFFER = 'Buffer' |
	ARRAY = 'Array' |
	MIXED = 'Mixed';