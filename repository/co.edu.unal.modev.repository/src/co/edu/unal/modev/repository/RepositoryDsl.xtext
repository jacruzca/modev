grammar co.edu.unal.modev.repository.RepositoryDsl with org.eclipse.xtext.common.Terminals

import "http://www.edu.co/unal/modev/entity/EntityDsl" as entity

generate repositoryDsl "http://www.edu.co/unal/modev/repository/RepositoryDsl"

RepositoriesModule:
	"repositories-module" name=ID "{"
		repositories+=Repository*
	"}"
;
	
FQN: ID ("." ID)*;
	
Repository:
	'repository' name=ID 'belongsTo' belongsTo=[entity::Entity|FQN]
	'{'
		(operations += RepositoryOperation)*
	'}'
;

RepositoryOperation:
	'operation' name=ID ('returnType' returnType=RepositoryReturnType)? 
	(	'{'
				(parameters += RepositoryParameter)+
		'}'
	)?
;

RepositoryReturnType:
	voidType=VOID | entityType=EntityType
;

RepositoryParameter:
	'parameter' name=STRING ('type' type=EntityType)?
;

EntityType:
	collectionType=EntityCollectionType | type=[entity::Entity | FQN] | literalType = LiteralType 
;

EntityCollectionType:
	'List<' listType=EntityType '>'
;

LiteralType:
	literal = STRING
;

enum VOID:
	VOID='void'
;